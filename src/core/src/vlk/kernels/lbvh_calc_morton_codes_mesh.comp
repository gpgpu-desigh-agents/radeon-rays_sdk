/**********************************************************************
Copyright (c) 2020 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/
#version 450
#extension GL_GOOGLE_include_directive : enable

#include "common.h"
#include "pp_common.h"
#include "bvh2.h"

#define RR_FLT_MIN 1.175494e-38
#define RR_FLT_MAX 3.402823e+38

// Full span BVH buffer.
layout(set = 0, binding = 0) buffer BVH
{
    BVHNode g_bvh[];
};

// Morton codes.
layout(set = 0, binding = 1) buffer MortonCodes
{
    uint g_morton_codes[];
};

// Indices.
layout(set = 0, binding = 2) buffer Indices
{
    uint g_indices[];
};

// Whole mesh AABB
layout(set = 0, binding = 3) buffer MeshAABB
{
    uint g_mesh_aabb[8];
};

layout(set = 1, binding = 0) buffer MeshIndices
{
    uint g_mesh_indices[];
};

layout(set = 1, binding = 1) buffer MeshVertices
{
    float g_mesh_vertices[];
};

// Push constants.
layout (push_constant) uniform PushConstants
{
    // Stride in the vertex buffer.
    uint g_vertex_stride;
    // Number of leaf indices.
    uint g_num_leafs;
};


layout (local_size_x = RR_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

void main()
{
    DECLARE_BUILTINS_1D;
    uint vertex_stride_in_floats = g_vertex_stride >> 2;
    for (int i = 0; i < PRIMITIVES_PER_THREAD; ++i)
    {
        //  Calculate linear triangle index.
        uint index = gidx * PRIMITIVES_PER_THREAD + i;

        if (index >= g_num_leafs)
        {
            return;
        }

        uint i0 = g_mesh_indices[(3 * index + 0)];
        uint i1 = g_mesh_indices[(3 * index + 1)];
        uint i2 = g_mesh_indices[(3 * index + 2)];

        // Get vertex 0.
        float x = g_mesh_vertices[i0 * vertex_stride_in_floats + 0];
        float y = g_mesh_vertices[i0 * vertex_stride_in_floats + 1];
        float z = g_mesh_vertices[i0 * vertex_stride_in_floats + 2];
        vec3 v0 = vec3(x, y, z);

        // Get vertex 1.
        x = g_mesh_vertices[i1 * vertex_stride_in_floats + 0];
        y = g_mesh_vertices[i1 * vertex_stride_in_floats + 1];
        z = g_mesh_vertices[i1 * vertex_stride_in_floats + 2];
        vec3 v1 = vec3(x, y, z);

        // Get vertex 2.
        x = g_mesh_vertices[i2 * vertex_stride_in_floats + 0];
        y = g_mesh_vertices[i2 * vertex_stride_in_floats + 1];
        z = g_mesh_vertices[i2 * vertex_stride_in_floats + 2];
        vec3 v2 = vec3(x, y, z);

        Aabb aabb = calculate_aabb_for_triangle(v0, v1, v2);

        Aabb mesh_aabb;
        mesh_aabb.pmin.xyz = Uint3ToFloat3(uvec3(g_mesh_aabb[0], g_mesh_aabb[1], g_mesh_aabb[2]));
        mesh_aabb.pmax.xyz = Uint3ToFloat3(uvec3(g_mesh_aabb[4], g_mesh_aabb[5], g_mesh_aabb[6]));
        vec3 mesh_extents = mesh_aabb.pmax - mesh_aabb.pmin;

        vec3 p = 0.5 * (aabb.pmin + aabb.pmax);

        p = (p - mesh_aabb.pmin) / mesh_extents;

        uint morton_code = calculate_morton_code(p);

        g_morton_codes[index] = morton_code;
        g_indices[index] = index;
    }
}

